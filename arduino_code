
#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>
#include <Button.h>

//temperature monitor
#define DHTPIN D7
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);

//WiFi info
const char* ssid     = "ssid";
const char* password = "password";

//MQTT info
const char* mqtt_server = "mqtt_server.example.org";
WiFiClient espClient;
PubSubClient client(espClient);

//Assign pins to relays and buttons
int wallFan = D3;  
int wallCompressor = D4; 
int buildingFan = D1; 
int buildingCompressor = D2;
int ledIndicator = D0;
Button startButton = Button(D6,PULLDOWN);
Button stopButton = Button(D5,PULLDOWN);

//assign variables
int statupDelay = 300; //in seconds
int targetTemp = 78;
int actualTemp;
int tempReading;
int fanTime = 0;
int compressorTime = 0;
int fanStatus = 0;
int compressorStatus = 0;
int airConditionerStatus = 0;
int ledIndicatorStatus = 0;
int maxRunTime = 2; // In hours
int runTime = 0;
int startButtonStatus;
int stopButtonStatus;
int compressorDelay = 5; // In minutes
int fanDelay = 5; // In minutes


void setup() {
    //start serial
    Serial.begin(115200);

    //start Wifi
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println("");
    Serial.println("WiFi connected");  
    Serial.println("IP address: ");
    Serial.println(WiFi.localIP());

    //Set pin mode
    pinMode(wallFan, OUTPUT);
    pinMode(wallCompressor, OUTPUT);
    pinMode(buildingFan, OUTPUT);
    pinMode(buildingCompressor, OUTPUT);
    pinMode(ledIndicator, OUTPUT);
    //pinMode(startButton, INPUT);
    //pinMode(stopButton, INPUT);

    //temperature monitor
    dht.begin();

    //turn off all relays
    digitalWrite(wallFan, HIGH);
    digitalWrite(wallCompressor, HIGH);
    digitalWrite(buildingFan, HIGH);
    digitalWrite(buildingCompressor, HIGH);
    digitalWrite(ledIndicator, LOW);


    Serial.println("start delay");
    //delay to not kill compressor on start up
    for (statupDelay; statupDelay > 0; statupDelay--) {
        Serial.println(statupDelay);
        delay(1000); 
    }
}

void loop() {
    //get temperature
    tempReading = dht.readTemperature(true);
    if (tempReading < 115){
        actualTemp = tempReading;
    }else{
        actualTemp = 0;
    }
    Serial.println("Actual Temp: ");
    Serial.println(actualTemp);
    
    
    //check buttons
    if(startButton.isPressed()){
        startButtonStatus = 1;
    }else{
        startButtonStatus = 0;
    }

    if(stopButton.isPressed()){
        stopButtonStatus = 1;
    }else{
        stopButtonStatus = 0;
    }


    //decriment runtime and set led status
    if (runTime > 0){
        runTime--;
        ledIndicatorStatus = 1;
    }

    //decrement fan time
    if (fanTime > 0){
        fanTime--;
    }

    //decrement compressor time
    if (compressorTime > 0){
        compressorTime--;
    }

    //turn off the compressor and start the timer
    if (compressorTime == 0 && compressorStatus == 1 && airConditionerStatus == 0){
        digitalWrite(wallCompressor, HIGH);
        digitalWrite(buildingCompressor, HIGH);
        compressorStatus = 0;
        compressorTime = (compressorDelay * 30);
        fanTime = (fanDelay *30);
        Serial.println("Compressor OFF");
    }

    //turn off the fan
    if (compressorStatus == 0 && fanTime == 0 && fanStatus == 1 && airConditionerStatus == 0){
        digitalWrite(wallFan, HIGH);
        digitalWrite(buildingFan, HIGH);
        fanStatus = 0;
        Serial.println("Fan OFF");
    }

    //start the shutdown process when the 2 hours is over
    if (runTime == 0){
        ledIndicatorStatus = 0;
        airConditionerStatus = 0;
        Serial.println("AC OFF");
    }

    //turn on the air conditioner
    if (airConditionerStatus == 1 && runTime > 0 && compressorTime == 0){
        digitalWrite(wallFan, LOW);
        digitalWrite(wallCompressor, LOW);
        digitalWrite(buildingFan, HIGH);
        digitalWrite(buildingCompressor, LOW);
        compressorTime = (compressorDelay * 30);
        fanStatus = 1;
        compressorStatus = 1;
        Serial.println("AC ON");
    }

    //compare temperature
    if (targetTemp < actualTemp){
        if (runTime > 0){
            airConditionerStatus = 1;
        }
    }else{
        airConditionerStatus = 0;
    }

    //check startbutton
    if (startButtonStatus == 1){
        runTime = (maxRunTime * 1800);
    }

    //check stopbutton
    if (stopButtonStatus == 1){
        runTime = 0;
    }

    //LED indicator
    if (ledIndicatorStatus == 1){
        digitalWrite(ledIndicator, HIGH);
    }else{
        digitalWrite(ledIndicator, LOW);
    }

    
    Serial.println("Run Time: ");
    Serial.println(runTime);
    Serial.println("AC Status: ");
    Serial.println(airConditionerStatus);
    Serial.println("Compressor Status: ");
    Serial.println(compressorStatus);
    Serial.println("Compressor Time: ");
    Serial.println(compressorTime);
    Serial.println("Fan Status: ");
    Serial.println(fanStatus);
    Serial.println("Fan Time: ");
    Serial.println(fanTime);
    Serial.println("LED: ");
    Serial.println(ledIndicatorStatus);
    Serial.println("-----------------------------------------------");
    //Delay each loop
    delay(2000);
    
}


